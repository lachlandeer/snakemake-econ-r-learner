# subworkflow - tables
#
# @yourname
#

# --- Importing Configuration Files --- #

configfile: "config.yaml"

# --- Sub Workflows --- #
subworkflow analysis:
   workdir: config["sub2root"] + config["ROOT"]
   snakefile:  config["sub2root"]+ config["src_analysis"] + "Snakefile"

# --- Build Rules --- #
## augment_solow      : construct a table of estimates for augmented solow model
rule augment_solow:
    input:
        script = config["src_tables"] + "tab02_augment_solow.R",
        models = analysis(expand(config["out_analysis"] +
                            "{iModel}_ols_{iSubset}.rds",
                            iModel = MODELS,
                            iSubset = DATA_SUBSET)
                            ),
    params:
        filepath   = config["out_analysis"],
        model_expr = "model_aug_solow*.rds"
    output:
        table = config["out_tables"] + "tab02_augment_solow.tex",
    log:
        config["log"] + "tables/tab02_augment_solow.Rout"
    shell:
        "Rscript {input.script} \
            --filepath {params.filepath} \
            --models {params.model_expr} \
            --out {output.table} \
            >& {log}"

## textbook_solow     : construct a table of regression estimates for textbook solow model
rule textbook_solow:
    input:
        script = config["src_tables"] + "tab01_textbook_solow.R",
        models = analysis(expand(config["out_analysis"] +
                            "{iModel}_ols_{iSubset}.rds",
                            iModel = MODELS,
                            iSubset = DATA_SUBSET)
                            ),
    params:
        filepath   = config["out_analysis"],
        model_expr = "model_solow*.rds"
    output:
        table = config["out_tables"] + "tab01_textbook_solow.tex"
    log:
        config["log"] + "tables/tab01_textbook_solow.Rout"
    shell:
        "Rscript {input.script} \
            --filepath {params.filepath} \
            --models {params.model_expr} \
            --out {output.table} \
            >& {log}"
